class GameField {
    field Array cells;
    field int rows, columns;

    constructor GameField new(int r, int c) {
        var int i;
        let rows = r;
        let columns = c;

        let cells = Array.new(r);
        let i = 0;
        while (i < r) {
            let cells[i] = Array.new(c); // 0 - empty, 1 - player1, 2 - player2
            let i = i + 1;
        }

        return this;
    }

    method void drawField() {
        var int x, y;
        var int i, j;
        var int cellColor;
        var Array row;
        let x = 143;
        let y = 15;

        let i = 0;
        while (i < rows) {
            let row = cells[i];
            let j = 0;
            while (j < columns) {
                let cellColor = row[j];

                do Screen.setColor(true);
                do Screen.drawRectangle(x, y, x+7, y+7);
                do Screen.setColor(false);
                do Screen.drawRectangle(x+1, y+1, x+6, y+6);

                do Screen.setColor(true);
                if (cellColor = 1) {
                    do Screen.drawLine(x+1, y+1, x+6, y+6);
                    do Screen.drawLine(x+6, y+1, x+1, y+6);
                }
                if (cellColor = 2) {
                    do Screen.drawRectangle(x+2, y+2, x+5, y+5);
                }

                let x = x + 7;
                let j = j + 1;
            }
            let x = 143;
            let y = y + 7;
            let i = i + 1;
        }

        // потом рисуем курсор игрока

        return;
    }
}